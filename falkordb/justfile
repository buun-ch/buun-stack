set fallback := true

export FALKORDB_NAMESPACE := env("FALKORDB_NAMESPACE", "falkordb")
export FALKORDB_VERSION := env("FALKORDB_VERSION", "v4.14.8")
export FALKORDB_STORAGE_SIZE := env("FALKORDB_STORAGE_SIZE", "8Gi")
export EXTERNAL_SECRETS_NAMESPACE := env("EXTERNAL_SECRETS_NAMESPACE", "external-secrets")
export PROMETHEUS_NAMESPACE := env("PROMETHEUS_NAMESPACE", "monitoring")
export MONITORING_ENABLED := env("MONITORING_ENABLED", "")

[private]
default:
    @just --list --unsorted --list-submodules

# Create FalkorDB namespace
create-namespace:
    @kubectl get namespace ${FALKORDB_NAMESPACE} &>/dev/null || \
        kubectl create namespace ${FALKORDB_NAMESPACE}
    @kubectl label namespace ${FALKORDB_NAMESPACE} \
        pod-security.kubernetes.io/enforce=baseline \
        pod-security.kubernetes.io/warn=restricted \
        --overwrite

# Delete FalkorDB namespace
delete-namespace:
    @kubectl delete namespace ${FALKORDB_NAMESPACE} --ignore-not-found

# Create FalkorDB password secret
create-password-secret:
    #!/bin/bash
    set -euo pipefail
    echo "Setting up FalkorDB password..."

    PASSWORD=$(just utils::random-password)

    if helm status external-secrets -n ${EXTERNAL_SECRETS_NAMESPACE} &>/dev/null; then
        echo "External Secrets available. Storing password in Vault and creating ExternalSecret..."
        just vault::put falkordb/auth password="$PASSWORD"
        gomplate -f falkordb-password-external-secret.gomplate.yaml -o falkordb-password-external-secret.yaml
        kubectl apply -f falkordb-password-external-secret.yaml
        echo "Waiting for password secret to be ready..."
        kubectl wait --for=condition=Ready externalsecret/falkordb-password-external-secret \
            -n ${FALKORDB_NAMESPACE} --timeout=60s
    else
        echo "External Secrets not available. Creating Kubernetes Secret directly..."
        kubectl delete secret falkordb-password -n ${FALKORDB_NAMESPACE} --ignore-not-found
        kubectl create secret generic falkordb-password -n ${FALKORDB_NAMESPACE} \
            --from-literal=redis-password="$PASSWORD"
        if helm status vault -n vault &>/dev/null; then
            just vault::put falkordb/auth password="$PASSWORD"
        fi
    fi
    echo "FalkorDB password setup completed"

# Delete FalkorDB password secret
delete-password-secret:
    @kubectl delete secret falkordb-password -n ${FALKORDB_NAMESPACE} --ignore-not-found
    @kubectl delete externalsecret falkordb-password-external-secret -n ${FALKORDB_NAMESPACE} --ignore-not-found

# Install FalkorDB
install:
    #!/bin/bash
    set -euo pipefail
    just create-namespace
    just create-password-secret

    METRICS_ENABLED="false"
    SERVICEMONITOR_ENABLED="false"

    if helm status kube-prometheus-stack -n ${PROMETHEUS_NAMESPACE} &>/dev/null; then
        if [ -z "${MONITORING_ENABLED}" ]; then
            if gum confirm "Enable Prometheus monitoring?"; then
                MONITORING_ENABLED="true"
            else
                MONITORING_ENABLED="false"
            fi
        fi
        if [ "${MONITORING_ENABLED}" = "true" ]; then
            METRICS_ENABLED="true"
            SERVICEMONITOR_ENABLED="true"
            kubectl label namespace ${FALKORDB_NAMESPACE} buun.channel/enable-monitoring=true --overwrite
        fi
    fi

    gomplate -f falkordb-values.gomplate.yaml -o falkordb-values.yaml
    helm upgrade --install falkordb ../charts/falkordb \
        -n ${FALKORDB_NAMESPACE} --create-namespace --wait \
        -f falkordb-values.yaml \
        --set metrics.enabled=${METRICS_ENABLED} \
        --set metrics.serviceMonitor.enabled=${SERVICEMONITOR_ENABLED} \
        --set metrics.serviceMonitor.labels.release=kube-prometheus-stack

    echo ""
    echo "FalkorDB installed successfully!"
    echo "Connection: falkordb.${FALKORDB_NAMESPACE}.svc.cluster.local:6379"

# Uninstall FalkorDB
uninstall:
    #!/bin/bash
    set -euo pipefail
    if gum confirm "Are you sure you want to uninstall FalkorDB?"; then
        helm uninstall falkordb -n ${FALKORDB_NAMESPACE} --wait --ignore-not-found
        just delete-password-secret
        just delete-namespace
        echo "FalkorDB uninstalled"
    else
        echo "Uninstall cancelled"
    fi

# Get FalkorDB password
get-password:
    @kubectl get secret falkordb-password -n ${FALKORDB_NAMESPACE} \
        -o jsonpath="{.data.redis-password}" | base64 -d
    @echo

# Execute redis-cli command in FalkorDB pod
[private]
redis-cli *args:
    #!/bin/bash
    set -euo pipefail
    PASSWORD=$(just get-password)
    kubectl exec -n ${FALKORDB_NAMESPACE} falkordb-0 -c falkordb -- \
        redis-cli -a "${PASSWORD}" --no-auth-warning {{ args }}

# Create a FalkorDB user with ACL
create-user username='' password='' allow='' deny='' graphs='':
    #!/bin/bash
    set -euo pipefail
    set -f  # Disable glob expansion for * handling

    USERNAME="{{ username }}"
    USER_PASSWORD="{{ password }}"
    ALLOW="{{ allow }}"
    DENY="{{ deny }}"
    GRAPHS="{{ graphs }}"

    while [ -z "$USERNAME" ]; do
        USERNAME=$(gum input --prompt="Username: " --placeholder="e.g., app-user")
    done

    if [ -z "$USER_PASSWORD" ]; then
        USER_PASSWORD=$(gum input --prompt="Password (empty for random): " --password --width=80)
        if [ -z "$USER_PASSWORD" ]; then
            USER_PASSWORD=$(just utils::random-password)
            echo "Generated password: $USER_PASSWORD"
        fi
    fi

    if [ -z "$ALLOW" ]; then
        echo "Allowed commands/categories (space-separated):"
        echo "  Categories: @all @read @write @fast @slow @connection @dangerous @admin"
        echo "  Graph: GRAPH.QUERY GRAPH.RO_QUERY GRAPH.DELETE GRAPH.LIST GRAPH.INFO ..."
        echo "  Redis: PING INFO DBSIZE KEYS SCAN EXISTS GET SET DEL ..."
        ALLOW=$(gum input --prompt="Allow: " --placeholder="e.g., @all or GRAPH.QUERY GRAPH.RO_QUERY INFO PING")
    fi

    if [ -z "$DENY" ]; then
        echo "Denied commands/categories (space-separated, optional):"
        echo "  e.g., @dangerous @admin FLUSHDB FLUSHALL DEBUG SHUTDOWN"
        DENY=$(gum input --prompt="Deny: " --placeholder="Leave empty for none")
    fi

    if [ -z "$GRAPHS" ]; then
        echo "Graph patterns (space-separated, * for all):"
        echo "  e.g., * or mygraph* or graph1* graph2*"
        GRAPHS=$(gum input --prompt="Patterns: " --value="*")
    fi

    # Build ACL command arguments as array
    ACL_ARGS=("ACL" "SETUSER" "$USERNAME" "on" ">$USER_PASSWORD")
    if [ -n "$ALLOW" ]; then
        for cmd in $ALLOW; do
            ACL_ARGS+=("+$cmd")
        done
    fi
    if [ -n "$DENY" ]; then
        for cmd in $DENY; do
            ACL_ARGS+=("-$cmd")
        done
    fi
    for pattern in $GRAPHS; do
        ACL_ARGS+=("~$pattern")
    done

    ADMIN_PASSWORD=$(just get-password)

    echo "Creating user '$USERNAME' with ACL rules..."
    RESULT=$(kubectl exec -n ${FALKORDB_NAMESPACE} falkordb-0 -c falkordb -- \
        redis-cli -a "${ADMIN_PASSWORD}" --no-auth-warning "${ACL_ARGS[@]}" 2>&1)

    if [[ "$RESULT" == *"ERR"* ]]; then
        echo "Error: $RESULT" >&2
        exit 1
    fi

    echo "Saving ACL to persistent storage..."
    SAVE_RESULT=$(kubectl exec -n ${FALKORDB_NAMESPACE} falkordb-0 -c falkordb -- \
        redis-cli -a "${ADMIN_PASSWORD}" --no-auth-warning ACL SAVE 2>&1)
    if [[ "$SAVE_RESULT" == *"ERR"* ]]; then
        echo "Warning: Failed to save ACL: $SAVE_RESULT" >&2
    fi

    echo "User '$USERNAME' created successfully"
    echo ""
    echo "Connection info:"
    echo "  Host: falkordb.${FALKORDB_NAMESPACE}.svc.cluster.local"
    echo "  Port: 6379"
    echo "  Username: $USERNAME"
    echo "  Graph patterns: $GRAPHS"

# List FalkorDB users
list-users:
    @just redis-cli ACL LIST

# Get FalkorDB user details
get-user username='':
    #!/bin/bash
    set -euo pipefail
    USERNAME="{{ username }}"
    if [ -z "$USERNAME" ]; then
        USERNAME=$(gum input --prompt="Username: " --placeholder="e.g., app-user")
    fi
    just redis-cli ACL GETUSER "$USERNAME"

# Delete a FalkorDB user
delete-user username='':
    #!/bin/bash
    set -euo pipefail
    USERNAME="{{ username }}"
    if [ -z "$USERNAME" ]; then
        USERNAME=$(gum input --prompt="Username to delete: " --placeholder="e.g., app-user")
    fi

    if [ "$USERNAME" = "default" ]; then
        echo "Error: Cannot delete the default user"
        exit 1
    fi

    if gum confirm "Delete user '$USERNAME'?"; then
        just redis-cli ACL DELUSER "$USERNAME"
        PASSWORD=$(just get-password)
        kubectl exec -n ${FALKORDB_NAMESPACE} falkordb-0 -c falkordb -- \
            redis-cli -a "${PASSWORD}" --no-auth-warning ACL SAVE
        echo "User '$USERNAME' deleted"
    else
        echo "Cancelled"
    fi

# Save ACL configuration to persistent storage
save-acl:
    @just redis-cli ACL SAVE
    @echo "ACL configuration saved"

# Get FalkorDB service URL
[private]
get-service-url:
    @echo "redis://falkordb.${FALKORDB_NAMESPACE}.svc.cluster.local:6379"

# Check FalkorDB health
health-check:
    #!/bin/bash
    set -euo pipefail
    echo "Checking FalkorDB health..."
    just redis-cli PING

# Test FalkorDB with basic graph operations
test:
    #!/bin/bash
    set -euo pipefail
    GRAPH_NAME="test_graph_$(date +%s)"

    echo "Testing FalkorDB"
    echo "Using graph: ${GRAPH_NAME}"
    echo

    echo "1. Creating nodes..."
    just redis-cli GRAPH.QUERY ${GRAPH_NAME} "CREATE (:Person {name: 'Alice', age: 30})"
    just redis-cli GRAPH.QUERY ${GRAPH_NAME} "CREATE (:Person {name: 'Bob', age: 25})"
    echo

    echo "2. Creating relationship..."
    just redis-cli GRAPH.QUERY ${GRAPH_NAME} "MATCH (a:Person {name: 'Alice'}), (b:Person {name: 'Bob'}) CREATE (a)-[:KNOWS {since: 2020}]->(b)"
    echo

    echo "3. Querying graph..."
    just redis-cli GRAPH.QUERY ${GRAPH_NAME} "MATCH (p:Person)-[:KNOWS]->(friend) RETURN p.name, friend.name"
    echo

    echo "4. Deleting test graph..."
    just redis-cli GRAPH.DELETE ${GRAPH_NAME}
    echo

    echo "Test completed successfully!"

# Clean up FalkorDB resources
cleanup:
    #!/bin/bash
    set -euo pipefail
    echo "This will delete all FalkorDB resources and secrets."
    if gum confirm "Are you sure you want to proceed?"; then
        echo "Cleaning up FalkorDB resources..."
        just vault::delete falkordb/auth || true
        echo "Cleanup completed"
    else
        echo "Cleanup cancelled"
    fi
